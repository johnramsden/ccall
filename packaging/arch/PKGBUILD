# Maintainer: John Ramsden <johnramsden [at] riseup [dot] com>
pkgname=ccall-git
pkgver=0.1.0
pkgrel=1
pkgdesc="ncurses program for calling sip number on click"
arch=('i686' 'x86_64')
url="https://github.com/johnramsden/ccall"
license=('BSD' 'custom:BSD 3 clause')
depends=("linphone")
makedepends=('git' 'cmake')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/johnramsden/ccall.git")
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname%-git}"
    git describe --long --tags | sed 's/-/.r/;s/-/./'
}

build() {
    cd "${srcdir}/${pkgname%-git}"
    cmake -DCMAKE_INSTALL_PREFIX=/usr .
    make
}

package() {
    cd "${srcdir}/${pkgname%-git}"
    make DESTDIR="${pkgdir}/" install
}
